dnl Process this file with autoconf to produce a configure script.
PACKAGE=twin
VERSION=0.4.5
AC_REVISION($Revision: 0.4.5 $)
AC_INIT(server/main.c)
AC_CONFIG_HEADER(include/autoconf.h)

AC_CONFIG_AUX_DIR(admin)

dnl shared libraries
AC_ARG_ENABLE(-shlibs,dnl
[  --enable--shlibs[=yes|no]              enable building shared libraries ],,
enable__shlibs=yes
)dnl

dnl shared modules
AC_ARG_ENABLE(-modules,dnl
[  --enable--modules[=yes|no]             enable building shared modules ],,
enable__modules=yes
)dnl

dnl custom malloc/free
AC_ARG_ENABLE(-alloc,dnl
[  --enable--alloc[=yes|no]               enable custom malloc/free routines ],,
enable__alloc=yes
)dnl

dnl unicode support
AC_ARG_ENABLE(-unicode,dnl
[  --enable--unicode[=yes|no]             enable building unicode-aware code ],,
enable__unicode=yes
)dnl

dnl socket server
AC_ARG_ENABLE(socket,dnl
[  --enable-socket[=yes|no]               enable remote socket connections (libTw) ],,
enable_socket=mod
)dnl

dnl socket compression
AC_ARG_ENABLE(socket_gz,dnl
[  --enable-socket-gz[=yes|no]            enable gzip compression on sockets ],,
enable_socket_gz=yes
)dnl

dnl thread safe libTw
AC_ARG_ENABLE(socket_pthreads,dnl
[  --enable-socket-pthreads[=yes|no]      enable thread safe libraries (requires pthread) ],,
enable_socket_pthreads=yes
)dnl

dnl support alien architectures
AC_ARG_ENABLE(socket_alien,dnl
[  --enable-socket-alien[=yes|no]         enable support for non-native architectures ],,
enable_socket_alien=yes
)dnl

dnl support Text Toolkit library
AC_ARG_ENABLE(tt,dnl
[  --enable-tt[=yes|no]                   enable Text Toolkit library (libTT) ],,
enable_tt=yes
)dnl

dnl Toolkit library: use twin (libTw) as display driver
AC_ARG_ENABLE(tt_hw_twin,dnl
[  --enable-tt-hw-twin[=yes|mod|no]       enable libTT to use twin (libTw) as display target ],,
enable_tt_hw_twin=mod
)dnl

dnl Toolkit library: use gtk as display driver
AC_ARG_ENABLE(tt_hw_gtk,dnl
[  --enable-tt-hw-gtk[=yes|mod|no]        enable libTT to use gtk as display target ],,
enable_tt_hw_gtk=mod
)dnl

dnl Toolkit library: use X11 as display driver
AC_ARG_ENABLE(tt_hw_x11,dnl
[  --enable-tt-hw-x11[=yes|mod|no]        enable libTT to use X11 as display target ],,
enable_tt_hw_x11=mod
)dnl

dnl Toolkit library: use xml files as display driver
AC_ARG_ENABLE(tt_hw_xml,dnl
[  --enable-tt-hw-xml[=yes|mod|no]        enable libTT to use xml files as display target ],,
enable_tt_hw_xml=mod
)dnl

dnl builtin window manager
AC_ARG_ENABLE(wm,dnl
[  --enable-wm[=yes|mod|no]               enable the builtin WM (absolutely needed!) ],,
enable_wm=mod
)dnl

dnl ~/.twinrc parser
AC_ARG_ENABLE(wm_rc,dnl
[  --enable-wm-rc[=yes|mod|no]            enable ~/.twinrc configuration parser ],,
enable_wm_rc=mod
)dnl

dnl use mmapped shared file for parser
AC_ARG_ENABLE(wm_rc_shmmap,dnl
[  --enable-wm-rc-shmmap[=yes|no]         enable mmapped shared file for parser ],,
enable_wm_rc_shmmap=yes
)dnl

dnl shrink memory when parser finished
AC_ARG_ENABLE(wm_rc_shrink,dnl
[  --enable-wm-rc-shrink[=yes|no]         enable shrinking memory when parser finished ],,
enable_wm_rc_shrink=yes
)dnl

dnl terminal emulator
AC_ARG_ENABLE(term,dnl
[  --enable-term[=yes|mod|no]             enable builtin terminal emulator ],,
enable_term=mod
)dnl

dnl use /dev/pts/* for terminal emulator
AC_ARG_ENABLE(term_devpts,dnl
[  --enable-term-devpts[=yes|no]          enable /dev/pts/* pseudo-ttys ],,
enable_term_devpts=yes
)dnl

dnl store server messages in "Messages" window
AC_ARG_ENABLE(printk,dnl
[  --enable-printk[=yes|no]               enable logging messages in Messages window ],,
enable_printk=yes
)dnl

dnl tty drivers
AC_ARG_ENABLE(hw-tty,dnl
[  --enable-hw-tty[=yes|mod|no]           enable tty drivers ],,
enable_hw_tty=mod
)dnl

dnl the Linux console driver
AC_ARG_ENABLE(hw-tty-linux,dnl
[  --enable-hw-tty-linux[=yes|no]         enable the Linux console driver (gpm mouse, /dev/vcsa)],,
enable_hw_tty_linux=yes
)dnl

dnl the twterm terminal driver
AC_ARG_ENABLE(hw-tty-twterm,dnl
[  --enable-hw-tty-twterm[=yes|no]        enable the twterm terminal driver ],,
enable_hw_tty_twterm=yes
)dnl

dnl the termcap terminal driver
AC_ARG_ENABLE(hw-tty-termcap,dnl
[  --enable-hw-tty-termcap[=yes|no]       enable the termcap/ncurses terminal driver ],,
enable_hw_tty_termcap=yes
)dnl

dnl the X11 driver
AC_ARG_ENABLE(hw-x11,dnl
[  --enable-hw-x11[=yes|mod|no]           enable the X11 driver ],,
enable_hw_x11=mod
)dnl

dnl the gfx driver
AC_ARG_ENABLE(hw-gfx,dnl
[  --enable-hw-gfx[=yes|mod|no]           enable the gfx (enhanced X11) driver ],,
enable_hw_gfx=mod
)dnl

dnl the twin driver
AC_ARG_ENABLE(hw-twin,dnl
[  --enable-hw-twin[=yes|mod|no]          enable the twin native driver ],,
enable_hw_twin=mod
)dnl

dnl the twdisplay driver
AC_ARG_ENABLE(hw-display,dnl
[  --enable-hw-display[=yes|mod|no]       enable the twdisplay client driver ],,
enable_hw_display=mod
)dnl

dnl the ggi driver
AC_ARG_ENABLE(hw-ggi,dnl
[  --enable-hw-ggi[=yes|mod|no]           enable the ggi driver (UNFINISHED) ],,
enable_hw_ggi=mod
)dnl

dnl
dnl Checks for programs.
dnl
AC_PROG_MAKE_SET
AC_PROG_CC
AC_PROG_CPP
dnl AC_PROG_YACC
dnl AC_PROG_LEX
AC_PROG_INSTALL
AC_PROG_LN_S

dnl
dnl Check for os-specific settings
dnl

AC_CACHE_CHECK(for OS, ac_cv_sys_uname, [
  ac_cv_sys_uname="`uname`"
])

AC_CACHE_CHECK(for shared library support, ac_cv_sys_shlibs, [
  if test "$ac_cv_sys_uname" = Linux -o "$ac_cv_sys_uname" = FreeBSD; then
    if test "$ac_cv_prog_gcc" = yes; then
      ac_cv_sys_shlibs=native
    fi
  fi
])

if test "$ac_cv_sys_shlibs" = native; then
  AC_MSG_RESULT([    this system is Linux/FreeBSD with GNU C
    assuming GNU C internally uses GNU ld
    using native rules to create shared libraries])
  AC_CHECK_PROG(AR, ar, ar,)
  AC_CHECK_PROG(RANLIB, ranlib, ranlib,)
  lt_cv_prog_gnu_ld=yes
elif test "$ac_cv_sys_shlibs" != no; then
  AC_MSG_RESULT([    this system is not Linux/FreeBSD with GNU C
    trying to use libtool to create shared libraries
configuring libtool...])
  #
  # disable static libraries in libtool... we build them manually
  #
  AC_CHECK_PROG(AR, ar, ar,)
  AC_DISABLE_STATIC
  AM_PROG_LIBTOOL
  AC_MSG_RESULT([...libtool configured])
  AC_MSG_CHECKING(for shared library support (again))
  if ./libtool --features | grep 'enable shared lib' >/dev/null; then
    ac_cv_sys_shlibs=libtool
  else
    ac_cv_sys_shlibs=no
  fi
  AC_MSG_RESULT([$ac_cv_sys_shlibs])
else
  AC_MSG_RESULT([    cache file says this system does not support shared libraries
    skipping libtool configuration])
fi

dnl
dnl Check for compiler characteristics.
dnl
AC_C_CONST
AC_CACHE_CHECK(for long long, ac_cv_type_long_long, AC_TRY_COMPILE( , [
  long long a = 0x1234567812345678ll;
  unsigned long long b = 0xfedcba12345678abull;
  
  b -= a;
  a += b;
],
  ac_cv_type_long_long=yes,
  ac_cv_type_long_long=no))

AC_C_INLINE

AC_CACHE_CHECK(for gcc-compatible 'static inline', ac_cv_c_static_inline, AC_TRY_COMPILE( [
  static inline int ret0(void) {
    return 0;
  }
] , [
  int a = ret0();
],
  ac_cv_c_static_inline=yes,
  ac_cv_c_static_inline=no))

AC_CACHE_CHECK(for gcc-compatible 'attribute((const))', ac_cv_c_attribute_const, AC_TRY_COMPILE( [
  static int ret0(void) __attribute__((const));
  static int ret0(void) {
    return 0;
  }
] , [
  int a = ret0();
],
  ac_cv_c_attribute_const=yes,
  ac_cv_c_attribute_const=no))

AC_CACHE_CHECK(for gcc-compatible 'attribute((packed))', ac_cv_c_attribute_packed, AC_TRY_COMPILE( , [
  struct sp {
    int a;
    char b;
  } __attribute__((packed));
  struct sp s[2];
],
  ac_cv_c_attribute_packed=yes,
  ac_cv_c_attribute_packed=no))

AC_CACHE_CHECK(for gcc-compatible i386 assembler, ac_cv_prog_gcc_i386_asm, AC_TRY_COMPILE( , [
#ifdef __i386__
  asm ("\n\tmovl %eax,%eax"
       "\n\tpushl %ecx"
       "\n\tpushl $1"
       "\n\tpopl %ecx"
       "\n\tpopl %ecx");
#else
  choke me
#endif
],
  ac_cv_prog_gcc_i386_asm=yes,
  ac_cv_prog_gcc_i386_asm=no))

dnl
dnl Checks for header files.
dnl
AC_HEADER_STDC
AC_HEADER_SYS_WAIT
AC_HEADER_TIME
AC_CHECK_HEADERS(crypt.h dirent.h dlfcn.h fcntl.h gpm.h ggi/ggi.h limits.h ltdl.h \
		 memory.h ncurses.h ncurses/ncurses.h termcap.h pthread.h shadow.h \
		 sys/filio.h sys/ioctl.h sys/mman.h sys/param.h sys/resource.h \
		 sys/select.h sys/timeb.h sys/ttydefaults.h sys/utsname.h sys/wait.h \
		 asm/page.h machine/param.h termio.h termios.h unistd.h zlib.h)


dnl
dnl Checks for typedefs, and structures
dnl
AC_TYPE_UID_T
AC_CHECK_TYPE(gid_t,int)
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_TYPE_SIGNAL
AC_STRUCT_TM
  

dnl
dnl Checks for library functions.
dnl
AC_FUNC_MEMCMP
AC_FUNC_MMAP
AC_FUNC_SETVBUF_REVERSED
dnl we never use the 'struct rusage' returned by wait3()
dnl AC_FUNC_WAIT3

AC_CHECK_FUNCS(ftime gettimeofday getspnam hstrerror initgroups lstat putenv setenv \
	       select strdup strerror strspn strstr scandir alphasort uname \
	       vprintf vsprintf vsnprintf wait3 wait4 grantpt unlockpt ptsname)

AC_CHECK_FUNC(crypt,,AC_CHECK_LIB(crypt,crypt))
AC_CHECK_FUNC(dlopen,,AC_CHECK_LIB(dl,dlopen))
AC_CHECK_LIB(ltdl,lt_dlopen)
AC_CHECK_FUNC(socket,,AC_CHECK_LIB(socket,socket))
AC_CHECK_FUNC(connect,,AC_CHECK_LIB(socket,connect))
AC_CHECK_FUNC(gethostbyname,,AC_CHECK_LIB(nsl,gethostbyname))
dnl AC_CHECK_FUNC(gethostbyname,,AC_CHECK_LIB(resolv,gethostbyname))

dnl try -lwsock32 (for mingw32 on Windows) if no socket-related functions found
if test "$ac_cv_func_select" = no; then
  AC_CHECK_LIB(wsock32,select)
  AC_CHECK_LIB(wsock32,socket)
  AC_CHECK_LIB(wsock32,connect)
  AC_CHECK_LIB(wsock32,gethostbyname)
fi

AC_CHECK_FUNC(pthread_self)
AC_CHECK_LIB(pthread,pthread_self) dnl FreeBSD would require AC_CHECK_LIB(c_r,pthread_self)
AC_CHECK_LIB(z, deflate)
AC_CHECK_LIB(gpm, Gpm_Open)
AC_CHECK_LIB(ggi, ggiOpen)

dnl libgpm if often linked against ncurses...
dnl no need for additional libraries in that case
AC_CHECK_LIB(gpm, tgetent)

LIBS=

AC_CHECK_LIB(termcap,tgetent,,AC_CHECK_LIB(ncurses,tgetent))

LIBS=

dnl test for X11
AC_PATH_X
if test "$have_x" = yes; then
  if test "$ac_x_includes"; then
    CF_HEADERX11="-I$ac_x_includes"
  fi
  if test "$ac_x_libraries"; then
    LD_LIBX11="-L$ac_x_libraries"
  fi
  LD_LIBX11="$LD_LIBX11 -lX11"
fi


dnl test for xpm
if test "$have_x" = yes; then
  ac_save_cpp="$ac_cpp"
  ac_save_link="$ac_link"
  ac_cpp="$ac_cpp $CF_HEADERX11"
  ac_link="$ac_link $LD_LIBX11"
fi
  AC_CHECK_HEADERS(X11/xpm.h)
  AC_CHECK_LIB(Xpm, XpmReadFileToPixmap)
if test "$have_x" = yes; then
  ac_cpp="$ac_save_cpp"
  ac_link="$ac_save_link"
fi

LIBS=

dnl test for gtk
AC_CHECK_PROG(gtk_config, gtk-config, yes)
if test "$ac_cv_prog_gtk_config" = yes; then
  ac_save_cpp="$ac_cpp"
  ac_save_link="$ac_link"
  ac_cpp="$ac_cpp `gtk-config --cflags`"
  ac_link="$ac_link `gtk-config --libs`"
fi
  AC_CHECK_HEADERS(gtk/gtk.h)
  AC_CHECK_LIB(gtk, gtk_init)
if test "$ac_cv_prog_gtk_config" = yes; then
  ac_cpp="$ac_save_cpp"
  ac_link="$ac_save_link"
fi

LIBS=

dnl test for xml2
dnl 
dnl AC_CHECK_PROG(xml2_config, xml2-config, yes)
dnl if test "$ac_cv_prog_xml2_config" = yes; then
dnl   ac_save_cpp="$ac_cpp"
dnl   ac_save_link="$ac_link"
dnl   ac_cpp="$ac_cpp `xml2-config --cflags`"
dnl   ac_link="$ac_link `xml2-config --libs`"
dnl fi
dnl   AC_CHECK_HEADERS(libxml/parser.h)
dnl   AC_CHECK_LIB(xml2, xmlInitParser)
dnl if test "$ac_cv_prog_xml2_config" = yes; then
dnl   ac_cpp="$ac_save_cpp"
dnl   ac_link="$ac_save_link"
dnl fi

dnl
dnl sanity checkings
dnl

if test "$ac_cv_type_long_long" != no; then
  AC_DEFINE(HAVE_LONG_LONG, 1, Define if you have the long long type)
fi

if test "$ac_cv_c_inline" != no; then
  AC_DEFINE(HAVE_INLINE, 1, Define if you have the inline keyword)
fi

if test "$ac_cv_c_static_inline" != no; then
  AC_DEFINE(HAVE_STATIC_INLINE, 1, Define if you have gcc-compatible 'static inline')
fi

if test "$ac_cv_c_attribute_const" != no; then
  AC_DEFINE(HAVE_ATTRIBUTE_CONST, 1, Define if you have gcc-compatible '__attribute__((const))')
fi

if test "$ac_cv_c_attribute_packed" != no; then
  AC_DEFINE(HAVE_ATTRIBUTE_PACKED, 1, Define if you have gcc-compatible '__attribute__((packed))')
fi


if test "$ac_cv_prog_gcc_i386_asm" = yes; then
  AC_DEFINE(HAVE_GCC_I386_ASM, 1, Define if you have gcc-compatible i386 assembler)
fi
GCC_I386_ASM="$ac_cv_prog_gcc_i386_asm"
AC_SUBST(GCC_I386_ASM)

GNU_LD="$lt_cv_prog_gnu_ld"
AC_SUBST(GNU_LD)


BUILD_PROG_TWIN=yes
if test "$ac_cv_func_gettimeofday" = no -a "$ac_cv_func_ftime" = no; then
  AC_MSG_WARN(missing gettimeofday() and ftime(): cannot compile twin server!)
  BUILD_PROG_TWIN=no
fi
AC_SUBST(BUILD_PROG_TWIN)

if test "$ac_cv_func_select" = no; then
  AC_MSG_ERROR(missing select(): cannot compile twin! Aborting.)
fi

if test "$ac_cv_func_gethostbyname" = yes -o "$ac_cv_lib_nsl_gethostbyname" = yes; then
  AC_DEFINE(HAVE_GETHOSTBYNAME, 1, Define if you have the gethostbyname function)
fi

if test "$ac_cv_func_memcmp_clean" = yes -o "$ac_cv_func_memcmp_working" = yes; then
  AC_DEFINE(HAVE_MEMCMP, 1, Define if memcmp() is available and works)
fi

if test "$ac_cv_func_putenv" = no -a "$ac_cv_func_setenv" = no; then
  AC_MSG_WARN(both putenv() and setenv() missing. twin will not set TERM and TWDISPLAY correctly!)
fi

if test "$ac_cv_func_wait3" = no -a "$ac_cv_func_wait4" = no; then
  AC_MSG_WARN(both wait3() and wait4() missing. twin will generate a lot of zombie processes!)
fi

if test "$ac_cv_func_crypt" = no -a "$ac_cv_lib_crypt_crypt" = no; then
  AC_MSG_WARN(missing crypt(): disabling twdm client!)
  LD_LIBCRYPT=
elif test "$ac_cv_lib_crypt_crypt" = yes; then
  LD_LIBCRYPT=-lcrypt
fi

dnl
dnl fixup configuration basing on autoconfigured stuff
dnl

dnl
dnl enable shared libraries/modules if possible
dnl

if test "$ac_cv_sys_shlibs" = no; then
  enable__modules=no
  enable__shlibs=no
fi

if test "$enable__modules" = yes; then
  can_enable__modules=no
  if test "$ac_cv_func_dlopen" = yes -o "$ac_cv_lib_dl_dlopen" = yes; then
    if test "$ac_cv_header_dlfcn_h" = yes; then
      AC_DEFINE(HAVE_DLOPEN, 1, Define if you have the dlopen() API)
      can_enable__modules=yes
    fi
  fi
  if test "$can_enable__modules" = no -a "$ac_cv_lib_ltdl_lt_dlopen" = yes -a "$ac_cv_header_ltdl_h" = yes; then
    AC_MSG_WARN([
	support for GNU libltdl is not functional yet, will not be used
])
    #AC_DEFINE(HAVE_LT_DLOPEN, 1, Define if you have the lt_dlopen() API)
    #can_enable__modules=yes
  fi
  enable__modules="$can_enable__modules"
fi

if test "$ac_cv_func_mmap_fixed_mapped" = no; then
  enable__alloc=no
  enable_wm_rc_shmmap=no
fi

if test "$ac_cv_func_socket" = no -a "$ac_cv_lib_socket_socket" = no -a "$ac_cv_lib_wsock32_socket" = no; then
  AC_MSG_WARN(missing socket(): disabling socket connections (libTw)!)
  enable_socket=no
fi

if test "$ac_cv_func_connect" = no -a "$ac_cv_lib_socket_connect" = no -a "$ac_cv_lib_wsock32_connect" = no; then
  AC_MSG_WARN(missing connect(): disabling socket connections (libTw)!)
  enable_socket=no
fi

if test "$ac_cv_func_gethostbyname" = no -a "$ac_cv_lib_nsl_gethostbyname" = no -a "$ac_cv_lib_wsock32_gethostbyname" = no; then
  AC_MSG_WARN(missing gethostbyname(): libTw will only accept numerical IP addresses!)
fi

if test "$ac_cv_lib_pthread_pthread_self" = no -o "$ac_cv_header_pthread_h" = no; then
  enable_socket_pthreads=no
fi

if test "$ac_cv_lib_z_deflate" = no -o "$ac_cv_header_zlib_h" = no; then
  enable_socket_gz=no
fi

if test "$ac_cv_func_grantpt" = no -o "$ac_cv_func_unlockpt" = no -o "$ac_cv_func_ptsname" = no; then
  enable_term_devpts=no
fi

if test "$ac_cv_lib_gpm_Gpm_Open" = no -o "$ac_cv_header_gpm_h" = no; then
  enable_hw_tty_linux=no
fi

if test "$ac_cv_lib_gpm_tgetent" = no -a "$ac_cv_lib_termcap_tgetent" = no -a "$ac_cv_lib_ncurses_tgetent" = no; then
  enable_hw_tty_termcap=no
fi

if test "$ac_cv_lib_ggi_ggiOpen" = no -o "$ac_cv_header_ggi_ggi_h" = no; then
  enable_hw_ggi=no
fi

if test "$have_x" != yes; then
  enable_hw_x11=no
  enable_tt_hw_x11=no
fi

if test "$have_x" != yes -o "$ac_cv_header_X11_xpm_h" = no -o "$ac_cv_lib_Xpm_XpmReadFileToPixmap" = no; then
  enable_hw_gfx=no
fi

if test "$ac_cv_header_gtk_gtk_h" = no -o "$ac_cv_lib_gtk_gtk_init" = no; then
  enable_tt_hw_gtk=no
fi

dnl if test "$ac_cv_header_libxml_parser_h" = no -o "$ac_cv_lib_xml2_xmlInitParser" = no; then
dnl   enable_tt_hw_xml=no
dnl fi

dnl
dnl rememeber any autodetected setting
dnl

OS="$ac_cv_sys_uname"
AC_SUBST(OS)

ECHO_E=echo
if test "`echo -e '\n'`" = ""; then
  ECHO_E='echo -e'
elif test "`echo '\n'`" = ""; then
  ECHO_E=echo
elif test "`/bin/echo -e '\n'`" = ""; then
  ECHO_E='/bin/echo -e'
elif test "`/bin/echo '\n'`" = ""; then
  ECHO_E=/bin/echo
fi
ECHO_C=
ECHO_N=
if test "`( echo '1\c'; echo 2 ) | grep 12`" = "12"; then
  ECHO_C='\c'
elif test "`( echo -n "1"; echo 2 ) | grep 12`" = 12; then
  ECHO_N='-n'
fi
if test -x /bin/bash; then
  BASH=/bin/bash
elif bash -c : 2>/dev/null; then
  BASH=bash
elif test -x "$BASH"; then
  :
else
 BASH=sh
fi
AC_SUBST(ECHO_E)
AC_SUBST(ECHO_N)
AC_SUBST(ECHO_C)
AC_SUBST(BASH)

if test "$ac_cv_prog_RANLIB"; then
  RANLIB="$ac_cv_prog_RANLIB"
elif test "ac_cv_prog_ac_ct_RANLIB"; then
  RANLIB="$ac_cv_prog_ac_ct_RANLIB"
else
  RANLIB=:
fi
if test "$ac_cv_prog_AR"; then
  AR="$ac_cv_prog_AR"
fi

AC_SUBST(RANLIB)
AC_SUBST(AR)

SYS_SHLIBS="$ac_cv_sys_shlibs"
AC_SUBST(SYS_SHLIBS)

if test "$ac_cv_lib_dl_dlopen" = yes; then
  LD_LIBDL=-ldl
elif test "$ac_cv_lib_ltdl_lt_dlopen" = yes; then
  LD_LIBDL=-lltdl
fi
AC_SUBST(LD_LIBDL)

if test "$ac_cv_lib_socket_socket" = yes -o "$ac_cv_lib_socket_connect" = yes; then
  LD_LIBSOCKET=-lsocket
elif test "$ac_cv_lib_wsock32_socket" = yes -o "$ac_cv_lib_wsock32_connect" = yes; then
  LD_LIBSOCKET=-lwsock32
fi
AC_SUBST(LD_LIBSOCKET)

if test "$ac_cv_lib_nsl_gethostbyname" = yes; then
  LD_LIBNSL=-lnsl
fi
AC_SUBST(LD_LIBNSL)

if test "$ac_cv_lib_pthread_pthread_self" = yes; then
  LD_LIBPTHREAD=-lpthread
fi
if test "$ac_cv_func_pthread_self" = no; then
  LD_LIBPTHREAD_WEAK="$LD_LIBPTHREAD"
fi
AC_SUBST(LD_LIBPTHREAD)
AC_SUBST(LD_LIBPTHREAD_WEAK)

if test "$ac_cv_lib_gpm_tgetent" = yes; then
  LD_LIBTERMCAP_IN_LIBGPM=-lgpm
fi
if test "$ac_cv_lib_termcap_tgetent" = yes; then
  LD_LIBTERMCAP=-ltermcap
elif test "$ac_cv_lib_ncurses_tgetent" = yes; then
  LD_LIBTERMCAP=-lncurses
elif test "$ac_cv_lib_gpm_tgetent" = yes; then
  LD_LIBTERMCAP=-lgpm
fi
AC_SUBST(LD_LIBTERMCAP_IN_LIBGPM)
AC_SUBST(LD_LIBTERMCAP)

if test "$ac_cv_header_X11_xpm_h" = yes; then
  CF_HEADERGFX="$CF_HEADERX11"
fi
if test "$ac_cv_lib_Xpm_XpmReadFileToPixmap" = yes; then
  LD_LIBGFX="$LD_LIBX11 -lXpm"
fi
AC_SUBST(CF_HEADERGFX)
AC_SUBST(LD_LIBGFX)

AC_SUBST(CF_HEADERX11)
AC_SUBST(LD_LIBX11)

AC_SUBST(LD_LIBCRYPT)

if test "$enable_tt_hw_gtk" != no; then
  CF_HEADERGTK="`gtk-config --cflags`"
  LD_LIBGTK="`gtk-config --libs`"
fi
AC_SUBST(CF_HEADERGTK)
AC_SUBST(LD_LIBGTK)


dnl if test "$enable_tt_hw_xml" != no; then
dnl   CF_HEADERXML="`xml2-config --cflags`"
dnl   LD_LIBXML="`xml2-config --libs`"
dnl fi
AC_SUBST(CF_HEADERXML)
AC_SUBST(LD_LIBXML)


dnl
dnl prepare conf/conf.auto using old conf/conf.current (or conf/conf.default)
dnl for non-specified, non-autodetectable things (CONF_OPT_*)
dnl

. conf/conf.default
if test -f conf/conf.current; then
  . conf/conf.current
fi
if test -f conf/conf.auto; then
  . conf/conf.auto
fi

CONF_=

for ac_FEATURE in `cat conf/conf.list`; do
  ac_feature=enable_`echo $ac_FEATURE | cut -d_ -f2- | sed 'y%ABCDEFGHIJKLMNOPQRSTUVWXYZ%abcdefghijklmnopqrstuvwxyz%'`
  eval "ac_feature_val=$`echo $ac_feature`"
  case "$ac_feature_val" in
    y | yes ) ac_feature_val=y ;;
    n | no  ) ac_feature_val=n ;;
    m | mod ) ac_feature_val=m ;;
    * )
      # unset / undetectable. use old value.
      eval "ac_feature_val=$`echo $ac_FEATURE`"
      ;;
  esac
  if test "$ac_feature_val"; then
    CONF_="$CONF_ $ac_FEATURE=$ac_feature_val;"
  fi
done
AC_SUBST(CONF_)

# all done


AC_OUTPUT(Makefile makeautoconf conf/conf.auto)


echo_shlibs="<none>"
echo_op=
echo_cl=
if test "$enable__shlibs" != no; then
  echo_op='('
  echo_cl=')'
  echo_shlibs="$SYS_SHLIBS"
fi

echo_feat=
for i in socket wm wm_rc term; do
  eval "echo_val=\${enable_$i}"
  if test "$echo_val" != no; then
    if test "$echo_val" = mod; then
      i="$echo_op""$i""$echo_cl"
    fi
    echo_feat="$echo_feat $i"
  fi
done
if test "$echo_feat" = ""; then
  echo_feat="<none>"
else
  echo_feat="`echo $echo_feat`"
fi

echo_hw=
for i in gfx x11 twin display tty ggi; do
  eval "echo_val=\${enable_hw_$i}"
  if test "$echo_val" != no; then
    if test "$echo_val" = mod; then
      i="$echo_op""$i""$echo_cl"
    fi
    echo_hw="$echo_hw $i"
  fi
done
if test "$echo_hw" = ""; then
  echo_hw="<none>"
else
  echo_hw="`echo $echo_hw`"
fi

echo_hw_tty=
if test "$enable_hw_tty" != no; then
  for i in linux twterm termcap; do
    eval "echo_val=\${enable_hw_tty_$i}"
    if test "$echo_val" != no; then
      if test "$echo_val" = mod; then
        i="$echo_op""$i""$echo_cl"
      fi
      echo_hw_tty="$echo_hw_tty $i"
    fi
  done
fi
if test "$echo_hw_tty" = ""; then
  echo_hw_tty="<none>"
else
  echo_hw_tty="`echo $echo_hw_tty`"
fi

echo_libs=
if test "$enable__unicode" != no; then
  echo_libs="$echo_libs $echo_op""libTutf""$echo_cl"
fi
if test "$enable_socket" != no; then
  echo_libs="$echo_libs $echo_op""libTw""$echo_cl"
fi
if test "$enable_tt" != no; then
  echo_libs="$echo_libs $echo_op""libTT""$echo_cl"
fi
if test "$echo_libs" = ""; then
  echo_libs="<none>"
else
  echo_libs="`echo $echo_libs`"
fi

echo_tt_hw=
for i in twin gtk x11 xml; do
  eval "echo_val=\${enable_tt_hw_$i}"
  if test "$echo_val" != no; then
    if test "$echo_val" = mod; then
      i="$echo_op""$i""$echo_cl"
    fi
    echo_tt_hw="$echo_tt_hw $i"
  fi
done
if test "$echo_tt_hw" = ""; then
  echo_tt_hw="<none>"
else
  echo_tt_hw="`echo $echo_tt_hw`"
fi

AC_MSG_RESULT([
Configuration:

  Items in parenthesis (...) will be built as shared libraries/modules

  Compiler:                   ${CC}
  Compiler flags:             ${CFLAGS}
  Shared libraries support:   ${echo_shlibs}
  Server features:            ${echo_feat}
  Server HW drivers:          ${echo_hw}
  Server hw_tty driver flags: ${echo_hw_tty}
  Libraries:                  ${echo_libs}
  TT library HW targets:      ${echo_tt_hw}
  Install path:               ${prefix}/bin, ${prefix}/lib/twin, ${prefix}/share/twin

to show/change other configuration settings, you can run one of:
'./configure <options>', 'scripts/Configure.sh <options>',
'make config' or 'make menuconfig'

twin is now (hopefully) configured.

As next step, you probably want to run 'make'.])

